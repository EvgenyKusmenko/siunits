/* (c) https://github.com/MontiCore/monticore */
package de.monticore.lang;

/**
 * This grammar defines SI units and other derived units such as 'm', 'km', 'km^2' or 'm*deg/(h^2*mg)'
 */

grammar SIUnits extends de.monticore.literals.MCCommonLiterals {

    /**
      * Every SIUnit is either a dimensionless unit such as 'deg' or a SIUnitExpression
      */
    SIUnit =
        SiUnitDimensionless | SIUnitExpression;

    /**
     * The SIUnitExpression describes an expression containing SIUnitBasics as primitives
     * so that every combination of 'x*y', 'x/y', 'x^n' and '(x)' is possible
     */
    interface SIUnitExpression;

    SIUnitMultExpression implements SIUnitExpression <10> =
        left:SIUnitExpression "*" right:SIUnitExpression;

    SIUnitOneDivExpression implements SIUnitExpression <20> =
        {cmpToken(1,"1")}? left:NatLiteral "/" right:SIUnitExpression;

    SIUnitDivExpression implements SIUnitExpression <30> =
        left:SIUnitExpression "/" right:SIUnitExpression;

    SIUnitBracketExpression implements SIUnitExpression <40> =
        "(" expression:SIUnit ")";

    SIUnitExponentExpression implements SIUnitExpression <50> =
        expression:SIUnitExpression "^" exponent:SignedNatLiteral;

    /**
     * The SIUnitBasics are the primitives of the SIUnitExpressions and contains
     * the basic SI units without prefixes such as 'm', 's' or 'kg' as well as the
     * basic SI units with prefixes such as 'km', 'mm' or 'ms'.
     * Other derived or officialy accepted units are also contained ('h', 'day', 'Ohm', ...)
     */
    interface SIUnitBasic extends SIUnitExpression;

    // see https://en.wikipedia.org/wiki/Metric_prefix
    // see https://en.wikipedia.org/wiki/SI_base_unit
    // see https://en.wikipedia.org/wiki/SI_derived_unit
    UnitBaseDimWithPrefix implements SIUnitBasic =
        { _input.LT(1).getText().matches("(Y|Z|E|P|T|G|M|k|h|da|d|c|m|n|p|f|a|z|y)?" +
            "(m|g|s|A|K|mol|cd|Hz|N|Pa|J|W|C|V|F|Ohm|S|Wb|T|H|lm|lx|Bq|Gy|Sv|kat)") }?
        unit:Name;

    // see https://en.wikipedia.org/wiki/Non-SI_units_mentioned_in_the_SI
    OfficallyAcceptedUnit implements SIUnitBasic =
        { _input.LT(1).getText().matches("min|h|day|ha|l|L|t|Au|Np|B|dB|eV|u") }?
        unit:Name;

    CelsiusFahrenheit implements SIUnitBasic =
        { _input.LT(2).getText().matches("F|C") }? "°" unit:Name;


    // see https://en.wikipedia.org/wiki/SI_derived_unit
    SiUnitDimensionless implements SIUnitBasic =
        "°" |
        { _input.LT(1).getText().matches("deg|rad|sr") }? unit:Name;
}
