/* (c) https://github.com/MontiCore/monticore */
package de.monticore.lang.literals;

/**
 * This grammar defines SI unit literals such as '3 m' or '2.5 km/h'
 * When the number is followed by L or F (Liter or Farad) an underscore between number and unit
 * is required, to differ from Float- and Longliterals. In all other cases the underscore is
 * optional, e.g. '3.2 _F', '3.2 m/F', '3.2 m' or '3.2 _m'.
*/

component grammar SIUnitLiterals extends de.monticore.lang.siunits.SIUnits,
                                         de.monticore.literals.MCCommonLiterals {

    // The unsigned SI unit literals
    SIUnitLiteral implements Literal <3> =
        longLiteral:BasicLongLiteral un:SIUnit |
        floatLiteral:BasicFloatLiteral un:SIUnit |
        num:NumericLiteral "_" un:SIUnit |
        num:NumericLiteral
        { !(_input.LT(1).getText().startsWith("F") ||
            _input.LT(1).getText().startsWith("L") ||
            _input.LT(1).getText().startsWith("l")) }? un:SIUnit
    ;

    // The signed SI unit literals
    SignedSIUnitLiteral implements SignedLiteral <3> =
        num:SignedNumericLiteral "_" un:SIUnit |
        longLiteral:SignedBasicLongLiteral un:SIUnit |
        floatLiteral:SignedBasicFloatLiteral un:SIUnit |
        num:SignedNumericLiteral
        { !(_input.LT(1).getText().startsWith("F") ||
            _input.LT(1).getText().startsWith("L") ||
            _input.LT(1).getText().startsWith("l")) }? un:SIUnit
    ;
}